add_library(MathLib INTERFACE)

set_property(TARGET MathLib PROPERTY CXX_STANDARD 20)
set_property(TARGET MathLib PROPERTY CXX_STANDARD_REQUIRED ON)

target_compile_options(MathLib INTERFACE # TODO: Change to the compile target
    $<$<CXX_COMPILER_ID:MSVC>:/W4;/wd4201>
    $<$<CXX_COMPILER_ID:GNU>:-Wall;-Wextra;-Wpedantic;-Wno-gnu-anonymous-struct;-Werror;>
    $<$<CXX_COMPILER_ID:Clang>:-Wall;-Wextra;-Wpedantic;-Wno-gnu-anonymous-struct;-Werror;>
)

target_compile_options(MathLib INTERFACE
    $<$<CXX_COMPILER_ID:MSVC>:/arch:AVX2>
    $<$<CXX_COMPILER_ID:GNU>:-mavx2>
    $<$<CXX_COMPILER_ID:Clang>:-mavx2>
)

set(IncludeDirectory "include/")

set(VectorDirectory "include/vector/")
set(VectorHeaderFiles Vector3D.h Vector2D.h Vector4D.h)
list(TRANSFORM VectorHeaderFiles PREPEND ${VectorDirectory})

set(VectorTemplateDefinitionFiles Vector2D.tpp Vector3D.tpp Vector4D.tpp)
list(TRANSFORM VectorTemplateDefinitionFiles PREPEND ${VectorDirectory})

set(MatrixDirectory "include/matrix/")
set(MatrixHeaderFiles Matrix2D.h Matrix3D.h Matrix4D.h)
list(TRANSFORM MatrixHeaderFiles PREPEND ${MatrixDirectory})

set(MatrixTemplateDefinitionFiles Matrix2D.tpp Matrix3D.tpp Matrix4D.tpp)
list(TRANSFORM MatrixTemplateDefinitionFiles PREPEND ${MatrixDirectory})

set(GeneralFiles MathTraits.h SimdTraits.h)
list(TRANSFORM GeneralFiles PREPEND ${IncludeDirectory})

target_sources(MathLib
    INTERFACE
    FILE_SET HEADERS
    BASE_DIRS ${IncludeDirectory}
    FILES
        ${VectorHeaderFiles}
        ${VectorTemplateDefinitionFiles}
        ${MatrixHeaderFiles}
        ${MatrixTemplateDefinitionFiles}
        ${GeneralFiles}
)


target_include_directories(
    MathLib
    INTERFACE
    "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>"
    "$<INSTALL_INTERFACE:include>"

)

add_custom_target(
    Math
    ALL
    SOURCES
    ${VectorHeaderFiles}
    ${VectorTemplateDefinitionFiles}
    ${MatrixHeaderFiles}
    ${MatrixTemplateDefinitionFiles}
    ${GeneralFiles}
)

source_group("Header Files\\vector" FILES ${VectorHeaderFiles})
source_group("Template Files\\vector" FILES ${VectorTemplateDefinitionFiles})
source_group("Header Files\\matrix" FILES ${MatrixHeaderFiles})
source_group("Template Files\\matrix" FILES ${MatrixTemplateDefinitionFiles})